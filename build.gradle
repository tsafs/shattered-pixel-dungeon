buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.2'
    }
}

allprojects {

    ext {
        appName = 'Shattered Pixel Dungeon'
        appPackageName = 'com.shatteredpixel.shatteredpixeldungeon'

        appVersionCode = 736
        appVersionName = '2.1.4'

        appJavaCompatibility = JavaVersion.VERSION_1_8

        appAndroidCompileSDK = 33
        appAndroidMinSDK = 14
        appAndroidTargetSDK = 33

        gdxVersion = '1.11.0'
        gdxControllersVersion = '2.2.4-SNAPSHOT'
        robovmVersion = '2.3.19'
    }
    version = appVersionName

    repositories {
        google()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
}

    
task writeProperties {
    doLast {
        def launchConfig = [
            "version": "0.2.0",
            "configurations": [
                [
                    "name": "Java Debug with Properties",
                    "type": "java",
                    "request": "launch",
                    "mainClass": "com.shatteredpixel.shatteredpixeldungeon.desktop.DesktopLauncher",
                    "preLaunchTask": "gradlew writeProperties",
                    "cwd": "\${workspaceFolder}",
                    "vmArgs": [
                        "-DSpecification-Title=$appName",
                        "-DImplementation-Title=$appPackageName",
                        "-DSpecification-Version=$appVersionName",
                        "-DImplementation-Version=$appVersionCode"
                    ],
                    "sourcePaths": [
                        "\${workspaceFolder}/core/src/main/assets",
                        "\${workspaceFolder}/desktop/src/main/assets"
                    ]
                ]
            ]
        ]
        new File("$projectDir/.vscode/launch.json").withWriter { writer ->
            writer.write(new groovy.json.JsonBuilder(launchConfig).toPrettyString())
        }
    }
}
